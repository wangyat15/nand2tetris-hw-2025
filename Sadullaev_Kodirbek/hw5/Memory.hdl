// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the      <-- THIS! Screen and Keyboard are given in the book
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];
    
    // RAM      0000 - 3FFF <=> 0000 0000 0000 0000 - 0011 1111 1111 1111   // hexadecimal <=> binary
    // SCREEN   4000 - 5FFF <=> 0100 0000 0000 0000 - 0101 1111 1111 1111   // hexadecimal <=> binary
    // KBD      6000        <=> 0100 0000 0000 0000

    PARTS:
    // select where to write
      DMux4Way(in=load, sel=address[13..14], a=selRam1, b=selRam2, c=selScreen, d=selKbd);
      Or(a=selRam1, b=selRam2, out=selRam);
      
      RAM16K(in=in, load=selRam, address=address[0..13], out=outRam);         // RAM
      Screen(in=in, load=selScreen, address=address[0..12], out=outScreen);   // Screen
      Keyboard(out=outKbd);                                                   // KBD - we don't ACTUALLY write in RAM..
    
    // select what to output
      Mux4Way16(a=outRam, b=outRam, c=outScreen, d=outKbd, sel=address[13..14], out=out);
}